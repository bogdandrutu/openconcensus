plugins {
    id "java-library"
    id "maven-publish"

    id "me.champeau.gradle.jmh"
    id "ru.vyarus.animalsniffer"
}

description = 'OpenTelemetry - zPages'
ext.moduleName = "io.opentelemetry.sdk.extension.zpages"

tasks['updateVersionInDocs'].configure {
    group("documentation")
    doLast {
        def readme = file("README.md")
        if (!readme.exists()) return

        def readmeText = readme.text
        def updatedText = readmeText
                .replaceAll("<version>\\d+\\.\\d+\\.\\d+</version>", "<version>${version}-alpha</version>")
                .replaceAll("(implementation.*io\\.opentelemetry:.*:)(\\d+\\.\\d+\\.\\d+)(?!-SNAPSHOT)(.*)", "\$1${version}-alpha\$3")
        readme.text = updatedText
    }
}

dependencies {
    implementation project(':api:all'),
            project(':sdk:all')

    testImplementation libraries.guava
    
    compileOnly 'com.sun.net.httpserver:http:20070405'
}

animalsniffer {
    // Don't check sourceSets.jmh and sourceSets.test
    sourceSets = [
            sourceSets.main
    ]
}
