pluginManagement {
    plugins {
        id "com.diffplug.spotless" version "5.7.0"
        id "com.github.ben-manes.versions" version "0.20.0"
        id "com.github.kt3k.coveralls" version "2.0.1"
        id "com.google.protobuf" version "0.8.8"
        id "com.jfrog.artifactory" version "4.13.0"
        id "com.jfrog.bintray" version "1.8.4"  // Need *specific* version.
        id "io.morethan.jmhreport" version "0.9.0"
        id "me.champeau.gradle.jmh" version "0.5.0"
        id "nebula.release" version "15.1.0"
        id "net.ltgt.errorprone" version "1.3.0"
        id "org.jetbrains.kotlin.jvm" version "1.4.10"
        id "org.unbroken-dome.test-sets" version "3.0.1"
        id "ru.vyarus.animalsniffer" version "1.5.2"
    }

    repositories {
        gradlePluginPortal()
        google()
    }
}

rootProject.name = "opentelemetry-java"
include ":opentelemetry-all",
        ":opentelemetry-api",
        ":opentelemetry-context",
        ":opentelemetry-extension-annotations",
        ":opentelemetry-extension-kotlin",
        ":opentelemetry-extension-runtime-metrics",
        ":opentelemetry-extension-trace-propagators",
        ":opentelemetry-extension-trace-utils",
        ":opentelemetry-exporter-jaeger",
        ":opentelemetry-exporter-jaeger-thrift",
        ":opentelemetry-exporter-logging",
        ":opentelemetry-exporter-otlp",
        ":opentelemetry-exporter-prometheus",
        ":opentelemetry-exporter-zipkin",
        ":opentelemetry-integration-tests",
        ":opentelemetry-integration-tests-tracecontext",
        ":opentelemetry-opencensus-shim",
        ":opentelemetry-opentracing-shim",
        ":opentelemetry-proto",
        ":opentelemetry-sdk-common",
        ":opentelemetry-sdk-metrics",
        ":opentelemetry-sdk-tracing",
        ":opentelemetry-sdk",
        ":opentelemetry-sdk-extension-async-processor",
        ":opentelemetry-sdk-extension-aws-v1-support",
        ":opentelemetry-sdk-extension-logging",
        ":opentelemetry-sdk-extension-otproto",
        ":opentelemetry-sdk-extension-resources",
        ":opentelemetry-sdk-extension-tracing-incubator",
        ":opentelemetry-sdk-extension-jaeger-remote-sampler",
        ":opentelemetry-sdk-extension-zpages",
        ":opentelemetry-sdk-testing",
        ":opentelemetry-bom",
        ":opentelemetry-perf-harness"

rootProject.children.each {
    it.projectDir = "$rootDir/" + it.name
            .replace("opentelemetry-integration-tests-", "integration-tests/")
            .replace("opentelemetry-exporter-", "exporters/")
            .replace("opentelemetry-extension-", "extensions/")
            .replace("opentelemetry-sdk-extension-", "sdk-extensions/")
            .replace("opentelemetry-sdk-", "sdk/")
            .replace("logging-", "logging/")
            .replace("opentelemetry-", "") as File
}

project(":opentelemetry-sdk").projectDir = "$rootDir/sdk/all" as File
